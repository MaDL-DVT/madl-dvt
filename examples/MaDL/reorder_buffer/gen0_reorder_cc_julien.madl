// ----------------------------------------------------------------
// ASNI Models
// -----------
// Model that should allow a proof of correspondance with the RTL 
// used during Sanne's internship. 
// The main idea is to use the output produced by the RTL as a challenge
// to the specification: can the specification mimic the just produced output?
// This is used to ensure that the selection of the next transaction is within
// the specification. Note that at the MaDL level this is left non-deterministic. 
// ----------------------------------------------------------------
// ----------------------------------------------------------------
// models a simple version of a reorder buffer and tracker
//
// the RD packet tracker consists of NUM_AID queues, each of TRACKER_DEPTH
// size.  at most TRACKER_DEPTH transactions can be outstanding per NUM_AID
//
// the reorder buffer receives NUM_AID inputs indicating which AID/TID pairs
// may progress.  it can buffer at most REORDER_SIZE messages.
//
// the Network macro models a network that can reorder msgs with different
// destinations.
//
// To eliminate the deadlock of gen0_reorder, we add a credit counter to ensure
// that no more than REORDER_SIZE messages can be in transit at once. This way,
// the reorder buffer always has enough size to store packets
// ----------------------------------------------------------------


// ----------------------------------------------------------------
// parameters
// ----------------------------------------------------------------
param int REORDER_SIZE = 2; // must be >= 2, because the loadbalancer needs at
                            // least two outputs

// deadlocks
// param int TRACKER_DEPTH = 3;
// param int CREDITS = REORDER_SIZE + 1;

// no deadlock
// param int TRACKER_DEPTH = 2;
// param int CREDITS = REORDER_SIZE + 1;

// deadlocks
// param int TRACKER_DEPTH = 2;
// param int CREDITS = REORDER_SIZE + 2;

// no deadlock
param int TRACKER_DEPTH = 2;
param int CREDITS = REORDER_SIZE;

param int LOG_NUM_AID = 2;
param int NUM_AID = 4;  // must be >= 3

param int LOG_NUM_TID = 2;
param int NUM_TID = 4; // must be >= 3

param int MAX_IN_TRANSIT_PER_TGT = 1;

// ----------------------------------------------------------------
// Message types 
// ----------------------------------------------------------------

struct ARpkt {
    aid : [LOG_NUM_AID-1:0]; // start small
    tid : [LOG_NUM_TID-1:0];
};


// ----------------------------------------------------------------
// General macro's
// ----------------------------------------------------------------

macro Reg(chan i) => chan o {
    let o := Queue(1, i);
};

const credit;

macro CreditCounter(int cnt, chan i) => chan o {
    chan q_in;
    let q_in, o := Fork(PatientSource(credit));
    Sink(CtrlJoin(Queue(cnt, q_in), i));
};

const tok;

macro Delay (chan i) => chan o {
    let o := CtrlJoin(i, Source(tok));
};
// ----------------------------------------------------------------
// ASNI Model 
// ----------------------------------------------------------------

macro split_by_aid (chan i) => bus<NUM_AID> o {
    bus<NUM_AID-2> tmp;

    let o[0], tmp[0] := Switch(i, ARpkt.aid == 0, otherwise);

    for (int idx = 0; idx < NUM_AID - 3; idx++) {
        let o[idx+1], tmp[idx+1] 
            := Switch(tmp[idx], ARpkt.aid == idx + 1, otherwise);
    };

    let o[NUM_AID-2], o[NUM_AID-1] :=
        Switch(tmp[NUM_AID-3], ARpkt.aid == NUM_AID-2, otherwise);
};

macro split_by_tid (chan i) => bus<NUM_TID> o {
    bus<NUM_TID-2> tmp;

    let o[0], tmp[0] := Switch(i, ARpkt.tid == 0, otherwise);

    for (int idx = 0; idx < NUM_TID - 3; idx++) {
        let o[idx+1], tmp[idx+1] 
            := Switch(tmp[idx], ARpkt.tid == idx + 1, otherwise);
    };

    let o[NUM_TID-2], o[NUM_TID-1] :=
        Switch(tmp[NUM_TID-3], ARpkt.tid == NUM_TID-2, otherwise);
};

macro RD_tracker(chan i) => bus<NUM_AID> o {

    bus<NUM_AID> by_aid := split_by_aid(i);

    for (int idx = 0; idx < NUM_AID; idx++) {
        let o[idx] := Queue(TRACKER_DEPTH, by_aid[idx]);
    };
};

pred id ( a : ARpkt, b : ARpkt ) {
    a.aid == b.aid && a.tid == b.tid
};

macro ID_resolver (bus<NUM_AID> input, chan nxt) => chan sel {
    let sel := MultiMatch(id, nxt, input);
};

macro MultiMerge(bus<NUM_AID> i) => chan o {
    bus<NUM_AID-2> tmp;
    let tmp[0] := Merge(i[0], i[1]);
    for (int idx = 0; idx < NUM_AID - 3; idx++) {
        let tmp[idx+1] := Merge(tmp[idx], i[idx+2]);
    };

    let o := Merge(tmp[1], i[3]);
};

macro MultiMergeTID(bus<NUM_TID> i) => chan o {
    bus<NUM_TID-2> tmp;
    let tmp[0] := Merge(i[0], i[1]);
    for (int idx = 0; idx < NUM_TID - 3; idx++) {
        let tmp[idx+1] := Merge(tmp[idx], i[idx+2]);
    };

    let o := Merge(tmp[1], i[3]);
};

macro RD_reorder_chk(chan i, chan nxt) => chan o {
    bus<REORDER_SIZE> to_buffer := LoadBalancer(i);
    bus<REORDER_SIZE> to_match;

    for (int idx = 0; idx < REORDER_SIZE; idx++) {
        let to_match[idx] := Reg(to_buffer[idx]);
    };

    let o := MultiMatch(id, nxt, to_match);
};

macro asni_chk (chan ARin, chan ntw_in, chan nxt_sel) 
               => chan ntw_out, chan AR_out {
    chan cc;
    chan AR := CtrlJoin(Queue(2,ARin), cc);
    let cc := CreditCounter(CREDITS, dealloc);
    chan to_tracker;
    let to_tracker, ntw_out := Fork(AR);
    chan dealloc;
    let AR_out, dealloc := 
        Fork(RD_reorder_chk(ntw_in,ID_resolver(RD_tracker(to_tracker),nxt_sel)));
};


// ----------------------------------------------------------------


// ----------------------------------------------------------------
// Simpel model of the A-network
// ----------------------------------------------------------------

macro Network (chan i) => chan o {
    bus<NUM_TID> by_tid := split_by_tid(Reg(i));
    bus<NUM_TID> pre_merge;
    for (int idx = 0; idx < NUM_TID; idx++) {
        let pre_merge[idx] := Queue(MAX_IN_TRANSIT_PER_TGT, by_tid[idx]);
    };

    let o := MultiMergeTID(pre_merge);
};

// ----------------------------------------------------------------

chan to_net, R := asni_chk(Source(ARpkt),from_net,Source(ARpkt));
chan from_net := Network(to_net);

Sink(R);




