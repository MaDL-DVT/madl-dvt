// models a reorder buffer and tracker without reordering in the network
//
// the RD packet tracker consists of NUM_AID queues, each of TRACKER_DEPTH
// size.  at most TRACKER_DEPTH transactions can be outstanding per NUM_AID
//
// the reorder buffer receives NUM_AID inputs indicating which AID/TID pairs
// may progress.  it can buffer at most REORDER_SIZE messages
//
// This model is NOT expected to have a deadlock, since the network is
// preserving all order.

param int TRACKER_DEPTH = 2;
param int REORDER_SIZE = 2; // must be >= 2, because the loadbalancer needs at
                            // least two outputs

param int LOG_NUM_AID = 2;
param int NUM_AID = 4;  // must be >= 3

param int LOG_NUM_TID = 2;
param int NUM_TID = 4;

// with a very small network, SMT can prove this deadlock free
// param int NETWORK_SIZE = 1;

// with a slightly bigger network, nuXmv is needed, but can prove deadlock
// freedom
param int NETWORK_SIZE = 2;
// with 3 or 4 there is a deadlock (checked with --ic3m)
// and with 5 as well. Is this expected behaviour?

struct ARpkt {
    aid : [LOG_NUM_AID-1:0]; // start small
    tid : [LOG_NUM_TID-1:0];
};

macro Reg(chan i) => chan o {
    let o := Queue(1, i);
};


macro split_by_aid (chan i) => bus<NUM_AID> o {
    bus<NUM_AID-2> tmp;

    let o[0], tmp[0] := Switch(i, ARpkt.aid == 0, otherwise);

    for (int idx = 0; idx < NUM_AID - 3; idx++) {
        let o[idx+1], tmp[idx+1] := Switch(tmp[idx], ARpkt.aid == idx + 1, otherwise);
    };

    let o[NUM_AID-2], o[NUM_AID-1] :=
        Switch(tmp[NUM_AID-3], ARpkt.aid == NUM_AID-2, otherwise);
};

macro RD_tracker(chan i) => bus<NUM_AID> o {

    bus<NUM_AID> by_aid := split_by_aid(i);

    for (int idx = 0; idx < NUM_AID; idx++) {
        let o[idx] := Queue(TRACKER_DEPTH, by_aid[idx]);
    };
};

macro MultiMerge(bus<NUM_AID> i) => chan o {
    bus<NUM_AID-2> tmp;
    let tmp[0] := Merge(i[0], i[1]);
    for (int idx = 0; idx < NUM_AID - 3; idx++) {
        let tmp[idx+1] := Merge(tmp[idx], i[idx+2]);
    };

    let o := Merge(tmp[1], i[3]);
};

const tok;

pred id ( a : ARpkt, b : ARpkt ) {
    a.aid == b.aid && a.tid == b.tid
};

macro RD_reorder(chan i, bus<NUM_AID> nxt) => chan o {
    bus<REORDER_SIZE> to_buffer := LoadBalancer(i);
    bus<REORDER_SIZE> to_match;

    for (int idx = 0; idx < REORDER_SIZE; idx++) {
        let to_match[idx] := Reg(to_buffer[idx]);
    };

    bus<NUM_AID> to_merge;
    let to_merge := MultiMatch(id, nxt, to_match);

    let o := MultiMerge(to_merge);
};


chan AR := Source(ARpkt);

macro Network (chan i) => chan o {
    let o := Queue(NETWORK_SIZE, i);
};

chan to_tracker, to_net := Fork(AR);
chan from_net := Network(to_net);

chan R := RD_reorder(from_net, RD_tracker(to_tracker));
Sink(R);

